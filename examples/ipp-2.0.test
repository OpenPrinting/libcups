#
# IPP/2.0 test suite.
#
# Copyright © 2022-2024 by OpenPrinting.
# Copyright © 2007-2017 by Apple Inc.
# Copyright © 2001-2006 by Easy Software Products. All rights reserved.
#
# Licensed under Apache License v2.0.  See the file "LICENSE" for more
# information.
#
# Usage:
#
#   ipptool [OPTIONS] -V 2.0 -tf FILENAME PRINTER-URI ipp-2.0.test
#
# Options:
#
#   -d NOPRINT=1              Disable the print tests.
#


# (REQUIRED) PWG 5101.1-2023: Media Standardized Names v2.1 (MSN)
DEFINE REQUIRE_MSN 1

# (REQUIRED) IETF STD 92: Internet Printing Protocol/1.1
INCLUDE "ipp-1.1.test"

# (REQUIRED) PWG 5100.1-2022: IPP Finishings v3.0 (MSN)
DEFINE REQUIRE_FIN 1
INCLUDE "pwg5100.1.test"

# (REQUIRED) PWG 5100.2-2001: IPP "output-bin" Attribute
DEFINE REQUIRE_OUTPUTBIN 1
INCLUDE "pwg5100.2.test"

# (RECOMMENDED) PWG 5100.7-2023: IPP Job Extensions v2.1 (JOBEXT)
INCLUDE "pwg5100.7.test"

# (RECOMMENDED) PWG 5100.9-2009: IPP Printer State Extensions v1.0 (STATE)
INCLUDE "pwg5100.9.test"

# (RECOMMENDED) RFC 3380: IPP: Job and Printer Set Operations
INCLUDE "rfc3380.test"

# (RECOMMENDED) RFC 3995: IPP: Notifications and Subscriptions
# (RECOMMENDED) RFC 3996: IPP: The 'ippget' Delivery Method for Event Notifications
INCLUDE "rfc3995-3996.test"

# (RECOMMENDED) RFC 3998: IPP: Job and Printer Administrative Operations
INCLUDE "rfc3998.test"

# PWG 5100.12-2024: IPP/2.x Fourth Edition (for IPP/2.0)
{
    NAME "PWG 5100.12-2024: Required Operations, Attributes, and Values"
    OPERATION Get-Printer-Attributes
    GROUP operation-attributes-tag
    ATTR charset attributes-charset utf-8
    ATTR naturalLanguage attributes-natural-language en
    ATTR uri printer-uri $uri
    ATTR name requesting-user-name $user
    ATTR mimeMediaType document-format application/octet-stream

    STATUS successful-ok

    # Baseline requirement/value
    EXPECT ipp-versions-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE "2.0"

    # Figure out capabilities
    EXPECT color-supported OF-TYPE boolean IN-GROUP printer-attributes-tag COUNT 1 WITH-VALUE true DEFINE-MATCH PRINTER_IS_COLOR
    EXPECT job-hold-until-supported OF-TYPE keyword|name IN-GROUP printer-attributes-tag DEFINE-MATCH PRINTER_HAS_JOB_HOLD_UNTIL
    EXPECT job-priority-supported OF-TYPE integer IN-GROUP printer-attributes-tag DEFINE-MATCH PRINTER_HAS_JOB_PRIORITY
    EXPECT job-sheets-supported OF-TYPE keyword|name IN-GROUP printer-attributes-tag DEFINE-MATCH PRINTER_HAS_JOB_SHEETS
    EXPECT media-col-supported OF-TYPE keyword IN-GROUP printer-attributes-tag DEFINE-MATCH PRINTER_HAS_MEDIA_COL
    EXPECT output-bin-supported OF-TYPE keyword|name IN-GROUP printer-attributes-tag DEFINE-MATCH PRINTER_HAS_OUTPUT_BIN
    EXPECT print-content-optimize-supported OF-TYPE keyword|name IN-GROUP printer-attributes-tag DEFINE-MATCH PRINTER_HAS_PRINT_CONTENT_OPTIMIZE
    EXPECT sides-supported OF-TYPE keyword IN-GROUP printer-attributes-tag DEFINE-MATCH PRINTER_HAS_SIDES

    # Table 2 - IPP Operations (IPP/2.0)
    EXPECT operations-supported OF-TYPE enum IN-GROUP printer-attributes-tag
    EXPECT operations-supported WITH-VALUE 0x0008 # Cancel-Job
    EXPECT operations-supported WITH-VALUE 0x0005 DEFINE-MATCH PRINTER_HAS_CREATE_JOB # Create-Job
	EXPECT operations-supported WITH-VALUE 0x0006 IF-DEFINED PRINTER_HAS_CREATE_JOB # Send-Document
	EXPECT operations-supported WITH-VALUE 0x003B IF-DEFINED PRINTER_HAS_CREATE_JOB # Close-Job
    EXPECT operations-supported WITH-VALUE 0x0009 # Get-Job-Attributes
    EXPECT operations-supported WITH-VALUE 0x000A # Get-Jobs
    EXPECT operations-supported WITH-VALUE 0x000B # Get-Printer-Attributes
    EXPECT operations-supported WITH-VALUE 0x0002 # Print-Job
    EXPECT operations-supported WITH-VALUE 0x0006 DEFINE-MATCH PRINTER_HAS_SEND_DOCUMENT # Send-Document
	EXPECT operations-supported WITH-VALUE 0x0005 IF-DEFINED PRINTER_HAS_SEND_DOCUMENT # Create-Job
	EXPECT operations-supported WITH-VALUE 0x003B IF-DEFINED PRINTER_HAS_SEND_DOCUMENT # Close-Job
    EXPECT operations-supported WITH-VALUE 0x0004 # Validate-Job

    # Table 5 - Updated IPP Printer Description Attributes (IPP/2.0)
    EXPECT color-supported OF-TYPE boolean IN-GROUP printer-attributes-tag COUNT 1

    EXPECT copies-default OF-TYPE integer IN-GROUP printer-attributes-tag COUNT 1 WITH-VALUE >0
    EXPECT copies-supported OF-TYPE rangeOfInteger IN-GROUP printer-attributes-tag

    EXPECT job-hold-until-default OF-TYPE keyword|name IN-GROUP printer-attributes-tag COUNT 1 WITH-VALUE-FROM job-hold-until-supported IF-DEFINED PRINTER_HAS_JOB_HOLD_UNTIL
    EXPECT job-hold-until-supported OF-TYPE keyword|name IN-GROUP printer-attributes-tag IF-DEFINED PRINTER_HAS_JOB_HOLD_UNTIL
    EXPECT job-hold-until-supported WITH-VALUE indefinite IF-DEFINED PRINTER_HAS_JOB_HOLD_UNTIL
    EXPECT job-hold-until-supported WITH-VALUE no-hold IF-DEFINED PRINTER_HAS_JOB_HOLD_UNTIL

    EXPECT job-priority-default OF-TYPE integer(1:100) COUNT 1 IN-GROUP printer-attributes-tag IF-DEFINED PRINTER_HAS_JOB_PRIORITY
    EXPECT job-priority-supported OF-TYPE integer(1:100) COUNT 1 IN-GROUP printer-attributes-tag IF-DEFINED PRINTER_HAS_JOB_PRIORITY

    EXPECT job-sheets-default OF-TYPE keyword|name COUNT 1 IN-GROUP printer-attributes-tag WITH-VALUE-FROM job-sheets-supported IF-DEFINED PRINTER_HAS_JOB_SHEETS
    EXPECT job-sheets-supported OF-TYPE keyword|name IN-GROUP printer-attributes-tag IF-DEFINED PRINTER_HAS_JOB_SHEETS
    EXPECT job-sheets-supported WITH-VALUE none IF-DEFINED PRINTER_HAS_JOB_SHEETS

    EXPECT media-default OF-TYPE no-value|keyword|name IN-GROUP printer-attributes-tag COUNT 1 WITH-VALUE "$MEDIA_REGEX"
    EXPECT ?media-ready OF-TYPE keyword|name IN-GROUP printer-attributes-tag WITH-ALL-VALUES "$MEDIA_REGEX"
    EXPECT media-supported OF-TYPE keyword|name IN-GROUP printer-attributes-tag WITH-ALL-VALUES "$MEDIA_REGEX"

    EXPECT orientation-requested-default OF-TYPE no-value|enum IN-GROUP printer-attributes-tag COUNT 1 WITH-VALUE 3,4,5,6,7
    EXPECT orientation-requested-supported OF-TYPE enum IN-GROUP printer-attributes-tag WITH-VALUE 3,4,5,6,7

    EXPECT output-bin-default OF-TYPE keyword|name IN-GROUP printer-attributes-tag COUNT 1 WITH-VALUE-FROM output-bin-supported
    EXPECT output-bin-supported OF-TYPE keyword|name IN-GROUP printer-attributes-tag

    EXPECT pages-per-minute OF-TYPE integer IN-GROUP printer-attributes-tag COUNT 1 WITH-VALUE >-1
    EXPECT pages-per-minute-color OF-TYPE integer IN-GROUP printer-attributes-tag COUNT 1 WITH-VALUE >-1 IF-DEFINED PRINTER_IS_COLOR
    EXPECT !pages-per-minute-color IF-NOT-DEFINED PRINTER_IS_COLOR

    EXPECT print-content-optimize-default OF-TYPE keyword COUNT 1 IN-GROUP printer-attributes-tag IF-DEFINED PRINTER_HAS_PRINT_CONTENT_OPTIMIZE WITH-VALUE-FROM print-content-optimize-supported
    EXPECT print-content-optimize-supported OF-TYPE keyword IN-GROUP printer-attributes-tag IF-DEFINED PRINTER_HAS_PRINT_CONTENT_OPTIMIZE WITH-ALL-VALUES "/^(auto|graphic|photo|text|text-and-graphic)$$/"

    EXPECT print-quality-default OF-TYPE enum IN-GROUP printer-attributes-tag COUNT 1 WITH-VALUE-FROM print-quality-supported
    EXPECT print-quality-supported OF-TYPE enum IN-GROUP printer-attributes-tag WITH-VALUE 3,4,5

    EXPECT printer-info OF-TYPE text(127) IN-GROUP printer-attributes-tag COUNT 1

    EXPECT printer-location OF-TYPE text(127) IN-GROUP printer-attributes-tag COUNT 1

    EXPECT printer-make-and-model OF-TYPE text(127) IN-GROUP printer-attributes-tag COUNT 1

    EXPECT printer-more-info OF-TYPE uri IN-GROUP printer-attributes-tag COUNT 1

    EXPECT printer-resolution-default OF-TYPE resolution IN-GROUP printer-attributes-tag COUNT 1 WITH-VALUE-FROM printer-resolution-supported
    EXPECT printer-resolution-supported OF-TYPE resolution IN-GROUP printer-attributes-tag

    EXPECT sides-default OF-TYPE keyword IN-GROUP printer-attributes-tag COUNT 1 WITH-VALUE-FROM sides-supported IF-DEFINED PRINTER_HAS_SIDES
    EXPECT sides-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-ALL-VALUES "/^(one-sided|two-sided-long-edge|two-sided-short-edge)$$/" IF-DEFINED PRINTER_HAS_SIDES
}
